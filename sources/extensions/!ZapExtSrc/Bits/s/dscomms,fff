; Zap Bits Command Extension
; $Id: dscomms,fff,v 1.3 1999/02/26 10:58:40 james Exp $
; Copyright (C) 1999 Zap Developers
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
;

; Commands written by Darren Salt

	GET	h.ZapBits
	GET	h.module
	GET	h.utils

	EXPORT	ds_toicon
	EXPORT	ds_sendurl
	EXPORT	ds_sendurlstring

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; TOICON
;
; Copy selection to writable icon
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	&	7:OR:1<<12:OR:1<<28

ds_toicon ROUT
	FNJSR	"r8"
	FNcall	Zap_GetSel, R7
	ADRCS	r0,nosel
	FNPULL	CS
	BCS	warn
	LDR	r0,[r9,#f_ptr]
	LDR	r4,[r9,#f_splito]
	LDR	r5,[r9,#f_splits]
	BL	get_workspace
	ADD	r3,r11,#wk_textbuffer
	CMP	r2,#255
	MOVHI	r2,#255
	STRB	r2,[r11,#wk_textlen]
0	TEQ	r1,r4
	ADDEQ	r1,r1,r5
	LDRB	r6,[r0,r1]
	ADD	r1,r1,#1
	STRB	r6,[r3],#1
	SUBS	r2,r2,#1
	BNE	%BT0
	MOV	r0,#0
	STRB	r0,[r3]
0	MOV	r0,#121				; wait until keys released
	MOV	r1,#0
	SWI	XOS_Byte
	FNRTS	VS
	BCC	%BT0
0	MOV	r0,#121				; wait for keypress
	MOV	r1,#0
	SWI	XOS_Byte
	FNRTS	VS
	BCS	%BT0
	TEQ	r1,#9				; Select?
	TEQNE	r1,#11				; Adjust?
	FNRTS	NE				; ...neither
	MOV	r8,r1
	MOV	r0,#21
	MOV	r1,#0
	SWI	XOS_Byte			; Flush kbd buffer
	FNRTS	VS
	TEQ	r8,#11
	MOV	r0,#138
	MOV	r1,#0
	MOVEQ	r2,#21
	SWIEQ	XOS_Byte			; If ADJUST, insert ^U
	LDR	r8,[r11,#wk_textlen]
 [ wk_textbuffer>0
	ADD	r11,r11,#wk_textbuffer
 ]
0	LDRB	r4,[r11],#1
	CMP	r4,#127
	CMPNE	r4,#31
	FNRTS	LS
	CMP	r4,#128
	MOVHS	r2,#0
	SWIHS	XOS_Byte
	MOVVC	r2,r4
	SWIVC	XOS_Byte			; Insert string (chr at a time)
	FNRTS	VS
	SUBS	r8,r8,#1
	BNE	%BT0
	FNRTS

nosel	StrZA	"zapbits_w_nosel","No selection"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; SENDURLSTRING
;
; Broadcast a URL (passed as a string) to all tasks
; Function - returns the URL at the given file offset
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	&	3<<3:OR:7:OR:1<<23:OR:2<<24

ds_sendurlstring ROUT
	TEQ	r2,#6
	BEQ	sendurlprompt
	FNJSR	"r2,r8-r10"	; keep in step with |ds_sendurl|
	TEQ	r2,#23
	MOVEQ	r2,r0
	BEQ	url_click
	MOV	r1,r0
	MOV	r2,#0
	MOV	r3,#0
0	LDRB	r0,[r1,r3]
	TEQ	r0,#0
	ADDNE	r3,r3,#1
	BNE	%BT0
	B	url_string
sendurlprompt
	ADR	r0,prompt$l
	FNjump	Zap_MiniPrompt, R2
prompt$l =	"URL",0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; SENDURL
;
; Broadcast a URL (at the cursor position) to all tasks
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

	&	0

ds_sendurl ROUT
	FNJSR	"r2,r8-r10"	; keep in step with |ds_sendurlstring|
	LDR	r2,[r10,#c_off]
url_click
	TEQ	r9,#0
	MOVEQ	r0,#0
	MOVEQ	r1,#3
	FNRTS	EQ
	LDR	r0,[r9,#f_len]
	CMP	r2,r0
	MOVHS	r0,#0
	MOVHS	r1,#3
	FNRTS	HS
	LDR	r1,[r9,#f_splits]
	FNcall	Zap_SplitBuffer, R3
	FNRTS	VS
	LDR	r1,[r9,#f_ptr]
	LDR	r3,[r9,#f_len]
url_string
	TEQ	r9,#0
	MOVEQ	r0,#0
	MOVEQ	r1,#3
	FNRTS	EQ
	ADD	r6,r2,#1
0	SUBS	r6,r6,#1
	LDRPLB	r0,[r1,r6]
	BMI	url_foundstart
	CMP	r0,#127
	CMPNE	r0,#'>'
	CMPNE	r0,#'<'
	CMPNE	r0,#')'
	CMPNE	r0,#'('
	CMPNE	r0,#'"'
	CMPNE	r0,#' '
	BHI	%BT0
url_foundstart
	ADD	r6,r6,#1
	MOV	r2,r6
	PUSH	"r12"
	TEQ	r2,r3
	LDRNEB	r5,[r1,r2]
	BEQ	notaURL
	TEQ	r5,#'U'
	MOVNE	r4,#34
	BNE	sendurl_table
	ADD	r2,r2,#1
	TEQ	r2,r3
	LDRNEB	r5,[r1,r2]
	BEQ	notaURL
	TEQ	r5,#'R'
	BNE	sendurl_trymailU
	ADD	r2,r2,#1
	TEQ	r2,r3
	LDRNEB	r5,[r1,r2]
	BEQ	notaURL
	TEQ	r5,#'L'
	BNE	sendurl_trymailU
	ADD	r2,r2,#1
	TEQ	r2,r3
	LDRNEB	r5,[r1,r2]
	BEQ	notaURL
	TEQ	r5,#':'
	BNE	sendurl_trymailU
	ADD	r2,r2,#1
	ADD	r6,r6,#4
	MOV	r4,#35
sendurl_table
	ADRL	r11,url_types-16
sendurl_loop
	LDRB	r12,[r11,r4,LSR #1]
	CMP	r12,#' '
	BLO	sendurl_close
	TEQ	r2,r3
	LDRNEB	r5,[r1,r2]
	MOVEQ	r5,#32
;	BEQ	notaURL
	TEQ	r12,#'#'
	BEQ	sendurl_wild_one
	TEQ	r12,#'$'
	BEQ	sendurl_wild_one_mail
	TEQ	r12,#'''
	BEQ	sendurl_wild_one_misc
	TEQ	r12,#','
	BEQ	sendurl_wild_todot_mail
	TEQ	r12,#'*'
	BEQ	sendurl_wild_many
	TEQ	r12,#'%'
	BEQ	sendurl_wild_many_mail
	TEQ	r12,#'~'
	BEQ	sendurl_wild_todot
	TEQ	r12,#'\'
	BEQ	sendurl_wild_one_end
	CMP	r5,#65
	RSBHSS	r14,r5,#90
	ORRHS	r5,r5,#32
	TEQ	r5,#':'
	ORREQ	r4,r4,#1
	TEQ	r12,r5
	ADDEQ	r4,r4,#2
	ADDEQ	r2,r2,#1
	BEQ	sendurl_loop
	AND	r12,r4,#30
	BIC	r14,r4,#30
	LDRB	r14,[r11,r14,LSR #1]
	CMP	r12,r14,LSL #1
	BHI	sendurl_trymail
	ADD	r4,r4,#32
	BIC	r14,r4,#30
	LDRB	r14,[r11,r14,LSR #1]
	TEQ	r14,#0
	BNE	sendurl_loop
	CMP	r4,#(url_mailto-url_types+16)*2
	BHS	notaURL
sendurl_trymail
	TST	r4,#1
	BNE	notaURL
sendurl_trymailU
	AND	r4,r4,#30
	TEQ	r4,#2
	MOV	r4,#(url_mailto-url_types+16)*2
	ADD	r4,r4,#2
	ADDNE	r4,r4,#2
	B	sendurl_table
sendurl_wild_todot_mail
	CMP	r5,#127
	CMPNE	r5,#'>'
	CMPNE	r5,#'<'
	CMPNE	r5,#')'
	CMPNE	r5,#'('
	CMPNE	r5,#'"'
	CMPNE	r5,#' '
	ADDHI	r4,r4,#2
	BHI	sendurl_loop
	TEQ	r5,#46
	SUBEQ	r4,r4,#2
	ADDEQ	r2,r2,#1
	BEQ	sendurl_loop
0	TEQ	r5,#58
	TEQNE	r5,#64
	BEQ	notaURL
	CMP	r5,#127
	CMPNE	r5,#'<'
	CMPNE	r5,#'('
	CMPNE	r5,#'"'
	CMPNE	r5,#' '
	ADDHI	r2,r2,#1
	BHI	sendurl_loop
	B	notaURL
sendurl_wild_one_end
	CMP	r5,#127
	CMPNE	r5,#'>'
	CMPNE	r5,#'<'
	CMPNE	r5,#')'
	CMPNE	r5,#'('
	CMPNE	r5,#'"'
	CMPNE	r5,#' '
	ADDHI	r4,r4,#2
	BHI	sendurl_loop
	CMP	r5,#127
	CMPNE	r5,#'<'
	CMPNE	r5,#'/'
	CMPNE	r5,#'('
	CMPNE	r5,#'"'
	CMPNE	r5,#' '
	SUBHI	r4,r4,#2
	ADDHI	r2,r2,#1
	BHI	sendurl_loop
	B	notaURL
sendurl_wild_todot
	TEQ	r5,#64
	MOVEQ	r4,#(url_mailto-url_types+16)*2
	ADDEQ	r4,r4,#6
	BEQ	sendurl_table
	TEQ	r5,#'/'
	ADDEQ	r4,r4,#2
	ADDEQ	r2,r2,#1
	BEQ	sendurl_loop
	CMP	r5,#'>'
	CMPNE	r5,#')'
	CMPNE	r5,#'"'
	CMPNE	r5,#' '
	ADDLS	r4,r4,#4
	BLS	sendurl_loop
	B	%BT0
sendurl_wild_one_mail
	TEQ	r5,#58
	TEQNE	r5,#46
	BEQ	notaURL
sendurl_wild_one_misc
	TEQ	r5,#64
	BEQ	notaURL
sendurl_wild_one
	CMP	r5,#33
	RSBHSS	r14,r5,#126
	BLO	notaURL
	TEQ	r5,#'>'
	TEQNE	r5,#'<'
	TEQNE	r5,#')'
	TEQNE	r5,#'('
	TEQNE	r5,#'"'
	ADDNE	r4,r4,#2
	ADDNE	r2,r2,#1
	BNE	sendurl_loop
	B	notaURL
sendurl_wild_many_mail
	CMP	r5,#58
	BEQ	notaURL
sendurl_wild_many
	ADD	r4,r4,#2
	LDRB	r12,[r11,r4,LSR #1]
	TEQ	r5,r12
	BEQ	sendurl_loop
	SUB	r4,r4,#2
	CMP	r5,#127
	CMPNE	r5,#'>'
	CMPNE	r5,#'<'
	CMPNE	r5,#')'
	CMPNE	r5,#'('
	CMPNE	r5,#'"'
	CMPNE	r5,#' '
	ADDHI	r2,r2,#1
	BHI	sendurl_loop
	B	notaURL
sendurl_close
	TEQ	r2,r3
	LDRNEB	r5,[r1,r2]
	BEQ	isaURL
	TEQ	r5,#58
	TEQNE	r5,#64
	TEQEQ	r12,#12
	BEQ	notaURL
	CMP	r5,#33
	RSBHSS	r14,r5,#126
	BLO	isaURL
	TEQ	r5,#'>'
	TEQNE	r5,#'<'
	TEQNE	r5,#')'
	TEQNE	r5,#'('
	TEQNE	r5,#'"'
	ADDNE	r2,r2,#1
	BNE	sendurl_close
isaURL	ADD	r4,r4,#2
	LDRB	r7,[r11,r4,LSR #1]
	PULL	"r12"
	SUB	r14,r2,r6
	TEQ	r7,#6
	ADDEQ	r14,r14,#6
	TEQ	r7,#14
	ADDEQ	r14,r14,#5
	TEQ	r7,#20
	TEQNE	r7,#23
	ADDEQ	r14,r14,#7
	LDR	r0,[r13]
	TEQ	r0,#23
	BEQ	zapURL
	PUSH	"r1-r3"
	MOV	r0,#6
	ADD	r3,r14,#5
	SWI	XOS_Module
	MOV	r14,r2
	PULL	"r1-r3"
	FNRTS	VS
	B	shortURL
zapURL	ADD	r0,r14,#1
	FNcall	Zap_Claim, R3
	FNRTS	VS
	SUB	r14,r0,#4
shortURL
	ADD	r3,r14,#4
	BL	get_workspace
	ADD	r2,r2,r1
	ADD	r6,r6,r1
	SUB	r14,r3,#4
	TEQ	r7,#6
	ADREQ	r7,url_addftp
	TEQ	r7,#14
	ADREQ	r7,url_addnews
	TEQ	r7,#20
	ADREQ	r7,url_addmailto
	TEQ	r7,#23
	ADREQ	r7,url_addhttp
	CMP	r7,#256
	BLO	gotURLtype
0	LDRB	r1,[r7],#1
	TEQNE	r1,#0
	STRNEB	r1,[r3],#1
	BNE	%BT0
gotURLtype
0	LDRB	r1,[r6],#1
	TEQ	r6,r2
	STRB	r1,[r3],#1
	BNE	%BT0
	MOV	r2,#0
	STRB	r2,[r3],#4
	LDR	r0,[r13]
	TEQ	r0,#23
	ADDEQ	r0,r14,#4
	MOVEQ	r1,#3
	FNRTS	EQ
; try to dispatch via AcornURI module...
	MOV	r7,r14
	MOV	r1,#4
	FNcall	Zap_ReadVar, r2
	MOV	r2,r0
	MOV	r0,#1
	ADD	r1,r7,#4
	SWI	&6E381			; XURI_Dispatch
	BVS	try_ANT_protocol
	TST	r0,#1
	BNE	try_ANT_protocol
	TEQ	r2,#0
	BEQ	uri_not_running
uri_retried
	STR	r3,[r7]
	LDR	r1,uri_reply
	ADR	r2,uri_replied
	MOV	r3,r7
	FNcall	Zap_ClaimMessage, r6
	FNRTS	VC
	MOV	r6,r0
	MOV	r0,#7
	MOV	r2,r7
	SWI	XOS_Module
	MOV	r0,r6
	FNRTV


url_addftp	= "ftp://",0
url_addnews	= "news:",0
url_addmailto	= "mailto:",0
url_addhttp	= "http://",0
	ALIGN

notaURL	PULL	"r12"
	LDR	r2,[r13]
	TEQ	r2,#23
	BLNE	beep
	MOV	r0,#0
	MOV	r1,#3
	FNRTVC		; problem with ClickSend - cause not determined :-(

uri_replied
	FNJSR	"r2,r8-r10"
	LDR	r0,[r1,#28]
	LDR	r14,[r11]
	TEQ	r0,r14
	MOVNE	r0,#0
	FNRTS	NE
	LDR	r0,[r1,#20]
	TST	r0,#1
	MOVNE	r7,r11
	BLNE	get_workspace
	BNE	try_ANT_protocol
	MOV	r0,#7
	MOV	r2,r11
	SWI	XOS_Module
	MOVVC	r0,#1
	FNRTS

url_types
	MACRO
	Url	$b,$t
	=	$b,$t
	%	15-:LEN:$t
	MEND
	Url	3,("file:/#")
	Url	2,("finger://#")
	Url	4,("ftp://#")
	Url	1,("ftp.'~#":CC::CHR:12:CC::CHR:6)
	Url	1,("gopher://#")
	Url	5,("http://#")
	Url	1,("https://#")
	Url	1,("mailto:$%@$":CC::CHR:44:CC::CHR:12)
	Url	5,("news:#")
	Url	2,("news.'~#":CC::CHR:12:CC::CHR:14)
	Url	1,("nntp://#")
	Url	1,("prospero://#")
	Url	1,("rlogin://#")
	Url	2,("telnet://#")
	Url	1,("tn3270://#")
	Url	2,("wais://#")
	Url	1,("www.'~#":CC::CHR:12:CC::CHR:23)
	%	16
url_mailto
	Url	1,("$%@$":CC::CHR:44:CC::CHR:12:CC::CHR:20)
	&	0

url_msg	&	&4AF80:OR:15<<28
uri_reply &	&4E383

uri_not_running
	MOV	r0,#0
	MOV	r1,#0
	MOV	r2,#0
	SWI	&6E383			; XURI_InvalidateURI
try_ANT_protocol
	MOV	r3,#0
	STR	r3,[r11,#20]
	ADD	r7,r7,#4
	STR	r7,[r11,#24]
	STR	r3,[r11,#28]
	STR	r3,[r11,#32]
	STR	r3,[r11,#36]
	STR	r3,[r11,#40]
	LDR	r0,url_msg
	MOV	r1,r11
	MOV	r2,#0
	MOV	r3,#44
	MOV	r4,#0
	ADR	r5,url_fail
	MOV	r6,#0
	FNcall	Zap_SendMessage
	MOVVC	r0,#1
	FNRTS

url_fail
	TEQ	r0,#0
	TEQNE	r0,#17
	TEQNE	r0,#19
	MOVNES	PC,r14
	FNJSR	"r2,r8-r10"
	MOV	r14,r0
	PUSH	"r0-r2"
	MOV	r0,r1
	ADD	r1,r1,#28
	SWI	XOS_ValidateAddress
	PULL	"r0-r2",CS
	FNRTS	CS
	LDR	r0,[r13]
	CMP	r0,#19
	BNE	try_URLOpen
	BL	get_workspace
	LDR	r7,[r13,#4]
	LDR	r0,[r7,#24]
	BL	run_uri_var
	BVS	try_URLOpen
	MOV	r0,#1
	LDR	r1,[r7,#24]
	SWI	&6E381			; XURI_Dispatch
	BVS	try_URLOpen
	TST	r0,#1
	BNE	try_URLOpen
	TEQ	r2,#0
	LDRNE	r7,[r7,#24]
	SUBNE	r7,r7,#4
	ADDNE	r13,r13,#12
	BNE	uri_retried
	MOV	r0,#0
	MOV	r1,#0
	MOV	r2,#0
	SWI	&6E383			; XURI_InvalidateURI
try_URLOpen
	MOV	r0,#7
	LDR	r2,[r1,#24]
	SUB	r2,r2,#4
	SWI	XOS_Module
	PULL	"r0-r2"
	CMP	r0,#19
	FNRTS	NE
	ADR	r2,aliasL
	LDMIA	r2,{r2-r5}
	STMIB	r7,{r2-r5}
	ADD	r6,r7,#20
0	LDRB	r2,[r6],#1
	TEQ	r2,#58
	BNE	%BT0
	MOV	r2,#0
	STRB	r2,[r6,#-1]!
	ADD	r0,r7,#6
	MVN	r1,#0
	MVN	r2,#0
	MOV	r3,#0
	SWI	XOS_ReadVarVal
	CMP	r2,#0
	BEQ	url_bounce
	RSB	r11,r2,#0
	MOV	r2,#58
	STRB	r2,[r6]
	LDR	r1,urlopen
	LDR	r2,urlopen+4
	STMIB	r7,{r1,r2}
	ADD	r1,r7,#12
	ADD	r2,r7,#20
0	LDRB	r3,[r2],#1
	TEQ	r3,#58
	STRNEB	r3,[r1],#1
	BNE	%BT0
	MOV	r3,#32
	ADD	r4,r7,#20
	MOV	r5,r4
0	CMP	r1,r4
	STRLTB	r3,[r1],#1
	BLT	%BT0
	MOV	r0,#10
	ADR	r1,scrap
	MOV	r2,#&B00
	ADD	r2,r2,#&28
0	LDRB	r3,[r5],#1
	TEQ	r3,#0
	BNE	%BT0
	SUB	r14,r5,r4
	ADD	r14,r14,#1
	CMP	r14,#250
	BLE	url_notinfile
	SWI	XOS_File
	FNRTS	VS
	ADR	r2,scrap
	LDMIA	r2,{r2-r5}
	ADD	r1,r7,#20
	STMIA	r1,{r2-r5}
url_notinfile
	ADD	r0,r7,#4
	SWI	XWimp_StartTask
	FNRTS
url_bounce
	ADR	r0,warn_urlbounce
	BL	warn
	FNRTS

aliasL		=	0,0,"Alias$"
urlopen		=	"URLOpen_" ; 16 chrs
aliasI		=	0,"Alias$"
uriopen		=	"Open_URI_" ; 16 chrs
scrap		=	"<Wimp$Scrap>",0
warn_urlbounce	StrZA	"zapbits_w_urlbounce","URL was unclaimed"

run_uri_var
	FNJSR	"r1-r7,r11"
	BL	get_workspace
	ADR	r2,aliasI
	LDMIA	r2,{r2-r5}
	STMIA	r11,{r2-r5}
	ADD	r6,r11,#16
	LDR	r5,[r7,#24]
0	LDRB	r2,[r5],#1
	TEQ	r2,#58
	STRNEB	r2,[r6],#1
	BNE	%BT0
	MOV	r2,#0
	STRB	r2,[r6]
	ADD	r0,r11,#1
	MVN	r1,#0
	MVN	r2,#0
	MOV	r3,#0
	MOV	r4,#4
	SWI	XOS_ReadVarVal
	MOV	r7,#0
	TEQ	r2,#0
	BEQ	run_uri_retv
	RSB	r0,r2,#1
	FNcall	Zap_Claim, r7
	BVS	run_uri_retv
	MOV	r7,r0
	ADD	r0,r11,#1
	MOV	r1,r7
	MVN	r2,r2
	MOV	r3,#0
	MOV	r4,#4
	SWI	XOS_ReadVarVal
	BVS	run_uri_retv
	MOV	r3,#0
	STRB	r3,[r7,r2]
	MOV	r2,r7
run_uri_handler
	SUB	r3,r2,#1
	LDRB	r0,[r3,#1]
	TEQ	r3,#0
	BEQ	run_uri_retv
0	LDRB	r0,[r3,#1]!
	TEQ	r0,#44
	TEQNE	r0,#0
	BNE	%BT0
	MOV	r0,#0
	STRB	r0,[r3]
	CMP	r2,r3
	MOVNE	r0,r2
	ADD	r2,r3,#1
	SWINE	XWimp_StartTask
	BVS	run_uri_handler
	TEQ	r0,#0
	BEQ	run_uri_handler
	MOV	r0,r7
	FNcall	Zap_Free, R7
	FNRTS
run_uri_retv
	MOV	r0,r7
	FNcall	Zap_Free, R7
	FNRTV


	END
