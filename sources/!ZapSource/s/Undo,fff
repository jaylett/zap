; Contains code to buffer undo data and act on it.
; $Id: Undo,fff,v 1.1.2.5 1999/07/17 23:07:00 ds Exp $

 GET h.ZapHeader
 GET h.TMTVars
 GET h.AlterTxt
 GET h.CreateFile
 GET h.Heap
 GET h.CreateWind

 IMPORT insertsomespaces
 IMPORT plot_caret
 IMPORT message_send
 IMPORT send_data_save
 IMPORT user_message_act
 IMPORT message_ack

 EXPORT undo_command
 EXPORT redo_command
 EXPORT yank_command
 EXPORT yank_cancel
 EXPORT add_yank_region
 EXPORT buffer_command
 EXPORT free_undo
 EXPORT turn_off_undo
 EXPORT paste_command
 EXPORT pastegc_command
 EXPORT gc_claimentity
 EXPORT gc_datarequest

ubuf_size		* &200		; increment size for undo buffer
udata			* &07		; undo data block increment mask

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; New format of undo buffers					;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; f_ptr		 =	start address of file
; f_ptr+f_bufl	 =	end of file / start of undo buffer
; f_ptr+f_blklen =	end of undo buffer / start of next file
;
; f_ulen	= length of valid data in the undo buffer
; f_undo	= offset of the undo pointer in the undo buffer
; (f_undop and f_undoptr no longer used)
;
; Undo buffer consists of a list of blocks of the following format:
;
;	<block length>	(currently 4 bytes - may be compressed later)
;	<block type>	(one byte)
;	<block data>	(variable length)
;	<block length>	(as above)
;
; Length of block at end enables backward tracing as well.
; Blocks are NOT word aligned.
;
; Block types are:
;
; &00:	Fast undo pointer. <data>=offset in undo buffer to jump to on
;	a fast undo. (skipping the full undo tree)
; &10:	Fast redo pointer. <data>=offset in undo buffer of block jumped
;	back from. Ie, this is the block to jump forward to.
; &01:	Insert text operation:
;	<data>= <offset in file text was inserted>
;		<in lined inserted data>
; &02:	Delete text operation:
;	<data> as for insert text except the deleted text is given.
; &03:	Replace text (forward) operation:
;	<data>=	<offset in file text was replaced>
;		<original text>
;		<replacement text> (both texts the same length)
;	(foward refers to the cursor being placed at the end after
;	replacement, so at the beginning after undoing).
; &04:	Replace text (backwards) operation:
;	<data> as above. Only difference is cursor position.
; &05:	Delete text operation:  (cursor at start)
;	<data> as for insert text except the deleted text is given.
; &06:	Insert text operation: (cursor at start)
;	<data>= <offset in file text was inserted>
;		<in lined inserted data>
; &07:	Start of a multiple operation marker. (see Zap_StartOp) No data.
; &17:	End of a muliple operation marker. (see Zap_StopOp) No data.

			^	0
undo_Length		#	4
undo_Type		#	1
undo_Offset		#	4	; used by everything that has data
undo_Data		#	0	; most have one (or two same-sized) data area(s)

undo_Type_fastundo	*	0x00
undo_Type_fastredo	*	0x10
undo_Type_insert	*	0x01
undo_Type_delete	*	0x02
undo_Type_replace_fwd	*	0x03
undo_Type_replace_bwd	*	0x04
undo_Type_delete_CAS	*	0x05
undo_Type_insert_CAS	*	0x06
undo_Type_startop	*	0x07
undo_Type_stopop	*	0x17

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Undo and redo							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;E R9=file
;X nulls yank / calls pre input / returns CS if no undo #

pre_undo
 FNJSR
 FNLDR R0,yank_len
 FNSTR R0,yank_off
 BL pre_input
 FNRTS VS
 LDR R0,[R9,#f_flags]
 AND R0,R0,#flags_UndoSupported
 EOR R0,R0,#flags_UndoSupported		; 0 undo 1 no undo
 CMP R0,#flags_UndoSupported
 FNRTS				; CS if no undo

;E R0=suggested num of chars R1=0 fast undo/1 full undo R9=file
;X this number of characters undone #

undo_command
 STMFD R13!,{R1-R11,R14}
 MOV R2,R0
 MOV R10,R1
 BL pre_undo			; check text ready for input
 LDMVSFD R13!,{R1-R11,PC}
 LDMCSFD R13!,{R1-R11,PC}	; no undo supported
 LDR R7,[R9,#f_uptr]
 LDR R6,[R9,#f_undo]
 LDR R8,[R9,#f_undop]
un7
; now...
 CMP R6,#0
 BMI un9b			; run out data (does this work?)

 TEQ R8,#0			; ???
 BNE un8			; found a command to undo
 MOV R0,R6
 BL un_backward
 MOVCS R6,#0
;was...
; BCS un9			; run out data (new 9a)
;now...
 BCS un9a			; run out data
 MOV R6,R0
 ADD R0,R7,R6			; this command
 LDR R14,[R0]			; get command

 MOV R1,R14,LSR #26		; command number
 AND R1,R1,#&17			; actual command number
 TEQ R1,#&17
 BEQ un22			; multiop (end)
 TEQ   R1,#&07			; mutilop start (woops)
 TEQNE R1,#&10			; fast redo (woops)
 BEQ un7			; loop
 TEQ R1,#0
 LDRNE R8,[R0,#4]
 BNE un7			; proper command
 TEQ R10,#0
 BNE un7			; fast undo not requested
 LDR R8,[R0,#4]			; new undop
 MOV R0,#&10
 ADD R1,R6,#8			; next command (at end of fast redo)
 BIC R6,R14,#&FE000000		; new undo ; FC000
 BL buffer_command		; buffer fast redo pointer
 BVC un7
 LDMFD R13!,{R1-R11,PC}
un8
 BL undo_invert_command		; find inverse command
 LDMVSFD R13!,{R1-R11,PC}
 MOV R10,R0			; save command
; STMFD R13!,{R1-R5}
 PUSH "R1-R5"
; BIC R0,R0,#&40 ; XXX
 BL do_command
; ORR R0,R0,#&40 ; XXX
; LDMFD R13!,{R1-R5}		; save bit3 set command
 PULL "R1-R5"			; ???
 MOVVC R0,R10
 BLVC buffer_command		; execute and buffer command
; LDMVSFD R13!,{R1-R11,PC}
 SUBVC R8,R8,R2			; reduce undop by number done
un9
 STRVC R6,[R9,#f_undo]
 STRVC R8,[R9,#f_undop]		; save vals
un9a
un9b
 LDMFD R13!,{R1-R11,PC}
 ;STR R6,[R9,#f_undo]
 ;STR R8,[R9,#f_undop]		; save vals
 ;SWI &107
 ;LDMFD R13!,{R1-R11,PC}
 ;LDMFD R13!,{R1-R11,PC}
un22
 BL undo_start_operation	; start a multiop
 MOV R0,#&07
 BL buffer_command		; add marker for command start
 BVS un24
; try next command...
un23
 MOV R0,R6
 BL un_backward			; move back
 MOVCS R6,#0			; reached start
 BCS un24			; out of data
 MOV R6,R0			; new command to undo
 ADD R0,R0,R7			; start address
 LDR R2,[R0,#4]			; number of chars
 MOV R8,R2
 BL undo_invert_command		; find inverse command
 BVS un24
 ANDS R14,R0,#7
 EORNES R14,R14,#7
 BEQ un24			; out of data
 MOV R10,R0			; save command
 PUSH "R1-R5"
; STMFD R13!,{R1-R5}
; BIC R0,R0,#&40 ; XXX
 BL do_command
; ORR R0,R0,#&40 ; XXX
; LDMFD R13!,{R1-R5}		; save bit3 set command
 PULL "R1-R5"			; ???
 BVS un24
 MOV R0,R10
 BL buffer_command		; execute and buffer command
 BVC un23			; try next command
un24
 BL undo_stop_operation		; display result
 MOVVC R0,#&17
 BLVC buffer_command		; add marker at the end
 MOVVC R8,#0			; finished this command
 BVC un9			; save new pointers
 LDMFD R13!,{R1-R11,PC}

;undowarning
; = "No more to undo",0

;E R0=suggested number of chars to redo R9=file
;X last undo redone #

redo_command
 FNJSR "R1-R11"
 MOV R2,R0			; save suggested num chars
 BL pre_undo			; check text ready for input
 FNRTS VS
; FNRTS CS		 	; no undo supported
 LDRCC R7,[R9,#f_uptr]
 LDRCC R6,[R9,#f_ulen]
 LDRCC R10,[R9,#f_undo]
 LDRCC R11,[R9,#f_undop]		; current undo/undop
 CMPCC R10,R6
; FNRTS CS			; nothing to redo
 BLCC redo_back
 CMPCC R10,R6
 FNRTS CS			; nothing to redo
 TEQ R0,#&17			; end of multiop
 BEQ un25			; redo whole of multiop
 TEQ R0,#7			; start of multiop
 BEQ un12			; skip it
 ADD R0,R7,R6
 LDR R8,[R0,#4]			; get length of command
 BL undo_invert_command		; find inverted command
; PUSH "R1-R5"
; BIC R0,R0,#&40 ; XXX
 BLVC do_command		; and do it
; ORR R0,R0,#&40 ; XXX
; PULL "R1-R5"
 BLVC unbuffer_command		; remove this command from the undo buffer
 FNRTS VS			; nothing to redo
 SUB R8,R8,R2			; number of chars left in this command
 ADD R11,R11,R2			; number of characters done in inverse
 CMP R8,#0
 BGT un10			; still some left
un12
 MOV R0,R10			; so increment undo to next one
 BL un_forward
 MOV R10,R0
 BCS un15
 ADD R0,R7,R10			; address of next instruction
 LDR R14,[R0]
 TST R14,#&3C000000
 BEQ un12			; not a proper instruction
un15
 MOV R11,#0			; move undo pointers forward
 BL redo_back			; move redo pointer back to valid command
un10
 MOV R0,R6
 BL un_forward
 MOV R6,R0			; and forward for new end of buffer
 CMP R10,R6
 MOVCS R10,R6
 MOVCS R11,#0			; clip overflowing undo pointers
 STR R6,[R9,#f_ulen]
 STR R10,[R9,#f_undo]
 STR R11,[R9,#f_undop]
 FNRTS

un25
 BL undo_start_operation
un26
 MOV R0,R10
 BL un_forward
 MOV R10,R0			; we've undone that one
 MOV R11,#0			; done none of the next
 BL redo_back

; was...
; not this...
; CMP R10,R6

; now...
; but this...?
 LDR R14,[R9,#f_ulen]
 CMP R10,R14			; was compare with R6 - bug.
				; hmm, this never seems to fire, though...

 BCS un27			; nothing left to redo...
 ADD R0,R7,R6			; command
 LDR R8,[R0,#4]			; get length of command
 MOV R2,R8			; amount to redo
 BL undo_invert_command		; find inverted command
 BVS un27
 ANDS R14,R0,#&07
 EORNES R14,R14,#7
 BEQ un27			; end of multiop
; PUSH "R1-R11"
; PUSH "R1-R5"			; ???
; BIC R0,R0,#&40 ; XXX
 BLVC do_command		; and do it
; ORR R0,R0,#&40 ; XXX
; PULL "R1-R5"			; ???
 BLVC unbuffer_command		; remove this command from the undo buffer
; BVS un27
 BVC un26				; loop to next command
; B un27
un27
 BL undo_stop_operation
; SWI &107
 BVC un12			; this command done
 FNRTS
;un27a
; BL undo_stop_operation
; SWI &107
; BVC un12			; this command done
; FNRTS

; IN R6=ulen R7=uptr R10=undo R11=undop
;OUT R6=first previous proper instruction (1-7)
;    R0=instruction number &17, R10/R11 updaded by fast redos

redo_back
 STMFD R13!,{R1,R14}
un11
 MOV R0,R6
 BL un_backward
 MOVCS R6,#0
 LDMCSFD R13!,{R1,PC}		; reached start of buffer !
 MOV R6,R0
 ADD R0,R7,R6			; address of previous instr
 LDR R14,[R0]			; previous instruction
 MOV R1,R14,LSR#26
 ANDS R0,R1,#&17		; instruction number
 BEQ un11			; a fast undo (shouldnt be there!!)
 TEQ R0,#&10
 LDMNEFD R13!,{R1,PC}		; found a proper instruction
 BIC R10,R14,#&FE000000		; a fast redo to this point
 MOV R11,#0
 B un11

local_yank_temp_col
 DCD 0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; YANK-related stuff...
;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;E R8-R10 input caret to yank at
;X last blocks before yank_off yanked #

yank_command
 STMFD R13!,{R6,R14}
 BL possiblystorecolumn
 FNLDR R6,yank_off		; current yank offset
 BL yank_cancel			; pre_input and cancel _off=_len
 BLVC yank_paste_command
 LDMVSFD R13!,{R6,PC}
 CMP R6,#0			; was an entry found
 FNSTR R6,yank_off,PL		; new yank offset
 LDMFD R13!,{R6,PC}

possiblystorecolumn
 FNJSR
 FNLDR R1,yank_off
 FNLDR R0,yank_len
 CMP R1,R0			; any yank to cancel?
 LDRCS R1,[R10,#c_col]
 STRCS R1,local_yank_temp_col
 FNRTS

;E R6=offset in yank buffer to start from R8-R10
;X R6=new yank_off /-ve if none. One yank command pasted. #

yank_paste_command
 STMFD R13!,{R1-R5,R7,R14}
 FNLDR R7,yank_ptr		; start of yank buffer
 BL start_operation		; may be multiple (eg ctrl K's)
 LDR R4,[R10,#c_off]		; paste offset
un17
 SUBS R6,R6,#8			; move back one
 BMI un30			; run out of data
 ADD R1,R7,R6			; block address
 LDR R5,[R1]			; get the file/address
 CMN R5,#1
 BEQ un17			; -1 => entry is dead
 BIC R0,R5,#3<<30		; actual address/offset
 LDR R2,[R1,#4]			; paramter (off/len)
 TST R5,#1<<31
 BNE un28			; actual block
 STMFD R13!,{R9}
 BL conv_file_off
 LDR R3,[R9,#f_uptr]
 LDMFD R13!,{R9}		; get R3=start of undo
 ADD R3,R3,R2			; entry address
 LDR R2,[R3,#4]			; length
 LDR R3,[R3,#8]			; data address
 B un29
un28
 MOV R3,R0			; address
un29
 BL insertsomespaces		; code seems ok.
 ADD R1,R4,R0			; start of region (<cur) & allow for spaces
 MOV R0,#&39			; insert block without buf/cur &79 for sel.
 BL execute_command		; perform the yank by inserting text
 BVS un30
 TST R5,#1<<30
 BNE un17			; more to do (eg ctrl K's)
un30
 BL stop_operation
 LDMFD R13!,{R1-R5,R7,PC}

;X any current yank cancelled and old text deleted #

yank_cancel
 STMFD R13!,{R1,R9,R14}
 FNLDR R1,yank_off
 FNLDR R0,yank_len
 CMP R1,R0			; any yank to cancel?
 LDMCSFD R13!,{R1,R9,PC}
 FNLDR R0,yank_file		; last yanked file
; CMP R0,#-1
; LDMEQFD R13!,{R1,R9,PC}
; BEQ noyank
 MOV R1,R9
 BL conv_file_off		; get R9
 CMP R1,R9			; in this file?
 LDMNEFD R13!,{R1,R9,PC}	; no: don't redo.
 MOV R0,#0
 STR R0,[R9,#f_undo]
 STR R0,[R9,#f_undop]		; pretend yank was an undo
 MOV R0,#1
 BL redo_command		; redo last command
 LDMVSFD R13!,{R1,R9,PC}
 LDR R0,[R9,#f_ulen]		; new buffer length
 STR R0,[R9,#f_undo]
 MOV R14,#0			; and reset undo pointers
 STR R14,[R9,#f_undop]		; (as they were by yank start)
 FNLDR R0,yank_len
 FNSTR R0,yank_off		; no yank on stack now

 LDR R0,local_yank_temp_col
 STR R0,[R10,#c_col]
 BL plot_caret

 LDMFD R13!,{R1,R9,PC}

;noyank
; BL Beep
; LDMFD R13!,{R1,R9,PC}

;E R8-R10 input caret to paste at
;X last deleted block pasted #

paste_command
 STMFD R13!,{R6,R14}
 FNLDR R6,yank_len
 FNSTR R6,yank_off		; start at end
 BL yank_paste_command		; insert the block
 LDMFD R13!,{R6,PC}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Global Clipboard support - SJA			   ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

message_claimentity * 15
message_datarequest * 16

;E R8-R10 input caret to paste at

pastegc_command
 FNLDR r0,opt_switches          ; opt_switches word
 ANDS r0,r0,#bit_got_gclip	; do we own the GC currently?
 BNE paste_command		; lr preserved for return

 FNJSR
 SUB r13,r13,#48		; some space for the message
 MOV r1,r13

 LDR r2,[r8,#0]
 STR r2,[r1,#20]
 MOV r2,#0

 STR r2,[r1,#24]		; internal handle (ignored)
 STR r2,[r1,#28]		; x position to drop on (irrelevant)
 STR r2,[r1,#32]		; y position to drop on (irrelevant)

 MOV r3,#4
 STR r3,[r1,#36]		; flags (send data from clipboard)

 LDR r0,[r9,#f_load]		; get load address
 BL calc_ftype			; returns in r0, or CC and r0=load addr
 MOVCC r0,#&1000
 SUBCC r0,r0,#3			; ask for data if it's an exec/load file
 STR r0,[r1,#40]
 MVN r3,#0
 STR r3,[r1,#44]		; end of filetype list (send native otherwise)

 MOV R0,#1
 FNSTR R0,opt_temporary
 MOV r6,r10			; pass as r11 to reply$l
 MOV r0,#message_datarequest	; r0 needs to include flags, so:
 ORR r0,r0,#(3:SHL:30 + 1:SHL:28) ; recorded, call reply code
 MOV r3,#48			; length

 ADR r5,reply$l
 BL message_send		; go

 ADD r13,r13,#48		; stack pointer back up
 FNRTS

reply$l
 CMP r0,#19
 BNE user_message_act
 FNJSR
 MOV r10,r11			; cursor block
 LDR r0,[r10,#c_wind]		; window offset
 BL conv_wind_off		; \X r8,r9
 BL paste_command		; paste in our internal clipboard
 FNRTS				; return

; Message_ClaimEntity
gc_claimentity
 FNJSR "r1"
 LDR r0,[r1,#20]		; is it for the clipboard?
 TST r0,#4
 FNRTS EQ

 LDR r0,[r1,#4]			; task handle of this message
 FNLDR r1,wimp_task		; Zap's task handle
 CMP r0,r1
 FNRTS EQ			; don't do anything on our message

 FNLDR r0,opt_switches          ; opt_switches word
 BIC r0,r0,#bit_got_gclip	; we no longer own it
 FNSTR r0,opt_switches          ; opt_switches word
 FNRTS

; Message_DataRequest
gc_datarequest
 FNJSR "r1-r10"
 LDR r0,[r1,#36]		; note the good design:
				; DataRq and ClaimEntity have the
 TST r0,#4			; type flags in different places ...
 FNRTS EQ			; not for the clipboard

 FNLDR r0,opt_switches          ; opt_switches word
 ANDS r0,r0,#bit_got_gclip
 FNRTS EQ			; we don't own it
 BL message_ack			; so it doesn't bounce

 PUSH "r1"			; store the message pointer so we can use it

 FNLDR r6,yank_len		; length of kill ring; ie last pointer
 FNLDR r7,yank_ptr
 ; Next section of code modified from yank_paste_command, above, to
 ; find the block which would be pasted if the user just did it local to Zap
dr1
 SUBS r6,r6,#8			; back one
 BMI pullr1andreturn
; FNRTS MI			; no more data
 ADD r1,r6,r7			; block address

 LDR r5,[r1]			; file/address
 CMN r5,#1
 BEQ dr1			; -1 => entry is dead

 BIC r0,r5,#(3:SHL:30)		; discard flags
 LDR r9,[r1,#4]			; parameter (offset/length)
 TST r5,#(1:SHL:31)
 BNE dr2			; actual block - so r9, r0 are set up
 ; Otherwise it's part of the file's undo buffer, so r0 is the file offset
 ; and r9 is the offset into the file's undo buffer
 PUSH "r9"			; store whatever is actually in r9
 BL conv_file_off		; r9 = file ptr
 LDR r3,[r9,#f_uptr]		; r3 = undo buffer in file
 PULL "r9"			; get it back again
 ADD r3,r3,r9			; actual entry
 LDR r9,[r3,#4]			; length
 LDR r0,[r3,#8]			; address
dr2
 MOV r8,r0			; address into r8, where we need it

 PULL "r1"			; restore message pointer

 ADD r2,r1,#20			; bit we need to write
 LDMIA r2,{r3-r6}		; use window etc. supplied by task
 ADR r2,gc_filename		; can't use file's leaf
 MOV R10,#&0F000000
 ORR R10,R10,#&00FF0000
 ORR r10,r10,#6			; r8/r9 are block delimiters + reply!
 MOV r7,#0			; don't tell me after save finished
; LDR R1,[R1,#8]			; get reference...
 BL send_data_save		; go

 FNRTS

pullr1andreturn
 PULL "r1"			; restore message pointer
 FNRTS

gc_filename
 = "GCScrap", 0

gc_claimclip
 FNJSR
 SUB r13,r13,#24		; space for the wimp message
 MOV r1,r13
 MOV r0,#message_claimentity	; includes required flags
 MOV r2,#0
 STR r2,[r1,#12]		; your_ref, and broadcast message
 MOV r3,#4
 STR r3,[r1,#20]		; flags: claiming the clipboard
 MOV r3,#24
 MOV r5,#0			; default reply code
 BL message_send
 ADD r13,r13,#24		; move back up
 FNLDR r0,opt_switches          ; opt_switches word
 ORR r0,r0,#bit_got_gclip	; set our internal variable
 FNSTR r0,opt_switches          ; opt_switches word
 FNRTS

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; New yank handling subs				   ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;E If b30 of R0 set then it's next in a kill sequence.
;  R0 BIC b30=0 => R8/R9=file, add last delete undo entry
;  R0 BIC b30<>0 => =address of region data blk R1=block length
;X Length added to yank_ptr buffer #

add_yank_region
 STMFD R13!,{R1-R7,R14}
 MOV R2,R0			; data pos start
 BICS R0,R2,#1<<30
 BNE ad17			; actual block
 BL get_file_off		; R0=file offset
 ADDVC R2,R2,R0			; data pos
 LDRVC R7,[R9,#f_uptr]
 LDRVC R0,[R9,#f_ulen]		; last entry
ad18
 BLVC un_backward
 LDMCSFD R13!,{R1-R7,PC}
 LDMVSFD R13!,{R1-R7,PC}
 LDR R1,[R7,R0]			; first word
 MOV R1,R1,LSR #26		; get command number
 AND R1,R1,#7			; command number
 TEQ R1,#2			; some kind of delete operation (2 or 5)?
 TEQNE R1,#5
 BNE ad18			; not yet found it
 MOV R3,R0			; offset in undo buffer
 B ad19
ad17
 ORR R2,R2,#1<<31
 MOV R3,R1
ad19
 FNLDR R4,yank_len		; buffer length
 ADD R5,R4,#8			; new length after adding entry
 ADD R1,R5,#63
 BIC R1,R1,#63			; round up
 FNLDR R0,yank_ptr
 BL heap_ensure
 FNSTR R0,yank_ptr,VC
 STRVC R2,[R0,R4]!
 STRVC R3,[R0,#4]			; set up next entry
 FNSTR R5,yank_len,VC		; new len
 FNSTR R5,yank_off,VC		; cancel any yank
 BLVC get_file_off
 FNSTR R0,yank_file,VC		; last yanked file
 BLVC gc_claimclip		; inform everyone that we've got the gc
 LDMFD R13!,{R1-R7,PC}

;E R0=off R7=undo buf R9
;X CC R0=next entry off / CS R0=end off |

un_forward
 STMFD R13!,{R1,R14}
 LDR R1,[R9,#f_ulen]
 CMP R0,R1
 LDMCSFD R13!,{R1,PC}
un1
 ADD R0,R0,#4
 CMP R0,R1
 LDMCSFD R13!,{R1,PC}
 LDR R14,[R7,R0]
 TST R14,#1<<31
 BEQ un1
 ADDS R0,R0,#0			; clc
 LDMFD R13!,{R1,PC}

;E R0=off R7=undo buf R9
;X CC R0=prvious entry / CS R0=0 |

un_backward
 FNJSR
un2
 CMP R0,#0
 LDMLEFD R13!,{PC}		; return with CS
 SUB R0,R0,#4
 LDR R14,[R7,R0]
 TST R14,#1<<31
 BEQ un2
 ADDS R0,R0,#0			; clc
 FNRTS

;E R0=command R1-R5=undo buffer data R7=undo buf R9=file
;X R7 upd and command buffered with new buffers claimed if b3=0 in R0 #

buffer_command
 STMFD R13!,{R1-R3,R6,R8-R11,R14}
 MOV R10,R0
; BIC R10,R0,#&FF000000		; Darren's conditionals...?  No.
 MOV R11,R1
 MOV R8,R2			; (LENGTH TT)
 LDR R6,[R9,#f_ulen]
 LDR R0,[R9,#f_ubufl]
 SUB R14,R0,R6			; free bytes in buffer
 CMP R14,#20
 BGE un3			; enough room
 MOV R0,R7
 ADD R1,R6,#ubuf_size		; increment size
 BL heap_ensure
 LDMVSFD R13!,{R1-R3,R6,R8-R11,PC}
 MOV R7,R0
 STR R7,[R9,#f_uptr]
 ADD R0,R6,#ubuf_size
 STR R0,[R9,#f_ubufl]		; update undo buffer vals
un3
 BIC R0,R10,#1<<3		; command number
 TEQ R0,#0
 BEQ buf_new_command
; was...
; CMP R0,#5			; !??? TT
; now...
 CMP R0,#7			; !??? TT
;
 BCS buf_new_command		; command not 1-4
 MOV R0,R6
 BL un_backward

 BCS buf_new_command		; no previous command
 LDR R14,[R7,R0]

; was...
; MOV R14,R14,LSR #26		;
; AND R14,R14,#&1F		; Get command number

; now...
 TST R14,#(1<<25)		;
 MOV R14,R14,LSR #26		;
 AND R14,R14,#&1F		; Get command number
 ORRNE R14,R14,#&40		;
;
 TEQ R10,R14
 BNE buf_new_command		; not the same as the previous command
 BIC R14,R10,#1<<3		; command number (!?)
 TEQ R14,#1
 TEQNE R14,#3
 TEQNE R14,#6
 BEQ buf_concat_top
 TEQ R14,#2			; ?
 TEQNE R14,#4			; ?
 TEQNE R14,#5			; ?
 BEQ buf_concat_bottom

buf_new_command
; new...
 CMP R6,#0			; otherwse address exceptions!?
 CMPPL R7,#0
 LDMMIFD R13!,{R1-R3,R6,R8-R11,PC}

;; BMI aproblem1$l		; nope
 ORR R14,R11,R10,LSL#26
 ORR R14,R14,#1<<31		; first word

; causes big problems...

; new...
 TST R10,#&40
 ORRNE R14,R14,#(1<<25)

 ADD R6,R7,R6			; address of entry
 STR R14,[R6],#4		; command and file offset - exception...!!!
 TEQ R10,#&10
 BEQ buf_new_end		; finished (fast redo)
 AND R14,R10,#7

; was...
; CMP R14,#5			; was 5 before X

; now...
 CMP R14,#7			; was 5 before X

 BCS buf_new_end		; finished (marker)
 STR R8,[R6],#4			; length of data ; (!LENGTH!TT)
 TEQ R10,#0
 BEQ buf_new_end		; finished
 TST R10,#1<<3
 BNE buf_no_claim		; bit3 set so no need to claim new buffers
 MOV R0,R8			; (LENGTH TT)
 BL heap_claim			; get heap buffer for data
 STRVC R0,[R6],#4			; buffer address
 MOVVC R1,R3			; source data
 MOVVC R2,R0
 MOVVC R3,R8			; (LENGTH TT)
 BLVC move_bytes			; copy data
 LDMVSFD R13!,{R1-R3,R6,R8-R11,PC}
 AND R14,R10,#&0F
 TEQ R14,#3			; test for replace
 TEQNE R14,#4
 BNE buf_new_end		; finished
 MOV R0,R8			; (LENGTH TT)
 BL heap_claim
 LDMVSFD R13!,{R1-R3,R6,R8-R11,PC}
 STR R0,[R6],#4
 MOV R1,R4
 MOV R2,R0
 MOV R3,R8			; (LENGTH TT)
 BL move_bytes			; second block of data for replace
 B buf_new_end
buf_no_claim
 STR R3,[R6],#4			; save data block address
 AND R0,R10,#7			; get command
 CMP R0,#3
 STRCS R4,[R6],#4		; original block for replace
buf_new_end
 SUB R6,R6,R7			; new end offset
 STR R6,[R9,#f_ulen]
 LDMFD R13!,{R1-R3,R6,R8-R11,PC}

;aproblem1$l
; SWI &107
; LDMFD R13!,{R1-R3,R6,R8-R11,PC}

buf_concat_top
 ADD R0,R7,R0			; previous command
 LDR R1,[R0]
 BIC R1,R1,#&FE000000		; file offset for insert/replace
 LDR R14,[R0,#4]		; length of data
 ; BIC R14,R14,#(1 << 31)       ; (LENGTH TT)
 ADD R1,R1,R14			; file offset of last char
 TEQ R1,R11
 BNE buf_new_command		; doesnt concat
 TST R10,#1<<3
 BNE buf_concat_top_no_claim
 MOV R6,R0			; alter previous command
 MOV R11,R14			; old len
 ADD R1,R11,R8			; new len
 ADD R1,R1,#udata
 BIC R1,R1,#udata		; goto next word boundry
 LDR R0,[R6,#8]			; address of data block
 BL heap_ensure			; make block big enough
 LDMVSFD R13!,{R1-R3,R6,R8-R11,PC}
 STR R0,[R6,#8]			; new block
 TEQ R10,#3
 BNE un20			; not a replace
 LDR R0,[R6,#12]
 BL heap_ensure			; ensure size of second block
 LDMVSFD R13!,{R1-R3,R6,R8-R11,PC}
 STR R0,[R6,#12]
un20
 LDR R0,[R6,#8]			; address
 MOV R1,R3			; source
 ADD R2,R0,R11
 MOV R3,R8
 BL move_bytes			; add on new data
 TEQ R10,#3			; is it a replace
 BNE un4
 LDR R0,[R6,#12]		; add to second block
 MOV R1,R4
 ADD R2,R0,R11
 MOV R3,R8
 BL move_bytes			; add on new data
un4
 ADD R0,R11,R8			; new block length
 STR R0,[R6,#4]
 LDMFD R13!,{R1-R3,R6,R8-R11,PC}
buf_concat_top_no_claim
 LDR R1,[R0,#8]			; address of data in previous command
 ADD R1,R1,R14			; address for ok concat
 TEQ R1,R3			; does it match the address given
 BNE buf_new_command		; no
 ADD R1,R8,R14			; new length of command
 STR R1,[R0,#4]			; save the new length
 LDMFD R13!,{R1-R3,R6,R8-R11,PC}

buf_concat_bottom
 ADD R0,R7,R0			; previous command
 LDR R1,[R0]
 BIC R1,R1,#&FE000000		; offset of old block
 SUB R1,R1,R8
 TEQ R1,R11			; can new block be added on start
 BNE buf_new_command		; no
 TST R10,#1<<3
 BNE buf_concat_bottom_no_claim
 MOV R6,R0			; address of block
 LDR R11,[R6,#4]		; old len ; (LENGTH TT)
 ; BIC R11,R11,#(1 << 31)       ; (LENGTH TT)
 ADD R1,R11,R8			; new len
 ADD R1,R1,#udata
 BIC R1,R1,#udata
 LDR R0,[R6,#8]
 BL heap_ensure
 LDMVSFD R13!,{R1-R3,R6,R8-R11,PC}
 STR R0,[R6,#8]			; enlarge first block
 TEQ R10,#4
 BNE un21			; not a replace
 LDR R0,[R6,#12]
 BL heap_ensure
 LDMVSFD R13!,{R1-R3,R6,R8-R11,PC}
 STR R0,[R6,#12]		; enlarge second block
un21
 STMFD R13!,{R3}		; save data
 LDR R1,[R6,#8]
 ADD R2,R1,R8
 MOV R3,R11
 BL move_bytes			; move up data in first block
 LDMFD R13!,{R1}		; get back data
 LDR R2,[R6,#8]
 MOV R3,R8
 BL move_bytes			; copy in new data
 TEQ R10,#4			; is it a replace
 BNE un19
 LDR R1,[R6,#12]
 ADD R2,R1,R8
 MOV R3,R11
 BL move_bytes			; move up data in second block
 MOV R1,R4
 LDR R2,[R6,#12]
 MOV R3,R8
 BL move_bytes			; copy in new data
un19
 ADD R0,R8,R11
 STR R0,[R6,#4]			; new length of block ; (LENGTH TT)
 LDR R0,[R6]
 SUB R0,R0,R8
 STR R0,[R6]			; new file offset
 LDMFD R13!,{R1-R3,R6,R8-R11,PC}
buf_concat_bottom_no_claim
 LDR R1,[R0,#8]			; old address
 SUB R1,R1,R8
 TEQ R1,R3			; does it match with new address
 BNE buf_new_command
 LDR R14,[R0,#4]
 ADD R14,R14,R8
 STR R14,[R0,#4]		; new command length ; (LENGTH TT)
 LDR R14,[R0]
 SUB R14,R14,R8
 STR R14,[R0]			; new offset of action
 STR R3,[R0,#8]			; new command start
 AND R14,R10,#7
 CMP R14,#3
 STRCS R4,[R0,#12]		; new second block for replace
 LDMFD R13!,{R1-R3,R6,R8-R11,PC}

;E R2=suggested num of chars to undo R6=undo R7=uptr R8=undop>0 <=
;X R0-R5=(bufferable) command required to undo R2 chars
;  and cursor moved to this point but not plotted (!?) #
;  R0=command number (5-bit) if out of range command 1-4

undo_invert_command
 STMFD R13!,{R6,R10-R11,R14}
 ADD R6,R7,R6			; address of command to undo
 LDR R0,[R6]			; command
 TST R0,#(1<<25)		;
 BIC R1,R0,#&FE000000		; file offset (35MB limit here :|)
 MOV R0,R0,LSR #26		; command number
 ORRNE R0,R0,#&40		; this line is a causal one for problem
 AND R0,R0,#&5F			; 6 bit command (?)
 ANDS R10,R0,#&07		; command
 ;LDMEQFD R13!,{R6,R10-R11,PC}	; out of range
 CMPNE R10,#7			; was 5...
 LDMEQFD R13!,{R6,R10-R11,PC}	; out of range
 ANDS R0,R0,#&50		; save concat bit and selected?
 MOVNE R2,R8			; undo whole command if top bit set
 CMP R2,R8
 MOVGT R2,R8			; clip to max number of chars
 LDR R3,[R6,#8]			; pointer to block
 TEQ R10,#2
 TEQNE R10,#4
 TEQNE R10,#5
 SUBNE R11,R8,R2		; num chars left after this undo
 LDREQ R11,[R6,#4]		; OR buf len
 SUBEQ R11,R11,R8		; less num chars not done to give offset
 ADD R1,R1,R11
 ADD R3,R3,R11
 TEQ R10,#3
 TEQNE R10,#4			; if not replace then
 EORNE R10,R10,#3		; change insert<->delete
 MOVEQ R4,R3			; else the new data becomes the old
 LDREQ R3,[R6,#12]
 ADDEQ R3,R3,R11		; and this is the new
 EOREQ R10,R10,#7		; opposite replace
 ORR R0,R0,R10			; complete command number
 ORR R0,R0,#1<<3		; and no need to rebuffer commands
; ORR R0,R0,#&40			; this line is a causal one for problem
 LDMFD R13!,{R6,R10-R11,PC}

;E R2=num of bytes to remove R6=buf off of command R7=uptr
;X command reduced by this number of bytes (and buffers freed)
;  note it only works for bit3 set commands and errors otherwise #

unbuffer_command
 STMFD R13!,{R1-R3,R6,R14}
 ADD R6,R7,R6			; address of the command
 LDR R0,[R6]
 MOV R3,R0,LSR #26		; top 6 bits - command number
 ANDS R1,R3,#&07		; command number
 LDMEQFD R13!,{R1-R3,R6,PC}	; command has no buffers
 CMP R1,#7			; was 5 :(
 LDMCSFD R13!,{R1-R3,R6,PC}	; command has no buffers
 TST R3,#1<<3			; don't complain if 0 because of yanks
 LDR R0,[R6,#4]			; num of bytes ; (LENGTH TT)
 SUBS R0,R0,R2			; number of bytes left
 LDMLEFD R13!,{R1-R3,R6,PC}	; none left
 STR R0,[R6,#4]			; reduce length of blk ; (LENGTH TT)
 TEQ R1,#2
 TEQNE R1,#4
 TEQNE R1,#5;
 LDMNEFD R13!,{R1-R3,R6,PC}	; this is all for a concat top
 LDR R0,[R6]
 ADD R0,R0,R2
 STR R0,[R6]			; increase file offset
 LDR R0,[R6,#8]
 ADD R0,R0,R2
 STR R0,[R6,#8]			; increment position of first buf
 TEQ R1,#4
 LDMNEFD R13!,{R1-R3,R6,PC}	; not a replace
 LDR R0,[R6,#12]
 ADD R0,R0,R2
 STR R0,[R6,#12]		; increment orig buf
 LDMFD R13!,{R1-R3,R6,PC}

;E R9=file
;X all undo buffer data freed and undo pointers zeroed #

free_undo
 STMFD R13!,{R1-R7,R14}
 BL free_yank			; free yank links to undo data
 LDMVSFD R13!,{R1-R7,PC}
 LDR R7,[R9,#f_uptr]
 LDR R6,[R9,#f_ulen]		; move backwards to help heap manager
 ADD R6,R7,R6
fr2
 SUB R0,R6,R7			; undo offset
 BL un_backward
 BCS fr10			; finished
 ADD R6,R7,R0			; next instruction
 LDR R5,[R6]

; now...
 MOV R1,#-1			; yes?

 MOV R5,R5,LSR #26		; top 6 bits
 TST R5,#1<<3			; command number

 BNE fr2			; no buffered data

; was...
; ANDS R5,R5,#&07
; BEQ fr2			; next data
; CMP R5,#4
; BHI fr2			; bad command/multiop marker

; now...
 ANDS R5,R5,#&07
 TEQNE R5,#7
 BEQ fr2			; next data, or bad command/multiop marker

 LDR R0,[R6,#8]
 STR R1,[R6,#8]
 BL heap_free
 LDMVSFD R13!,{R1-R7,PC}

 TEQ R5,#3
 TEQNE R5,#4
 BNE fr2			; not a replace

 LDR R0,[R6,#12]
 STR R1,[R6,#12]
 BL heap_free			; free second block
 BVC fr2
 LDMFD R13!,{R1-R7,PC}
fr10
 MOV R0,#0
 STR R0,[R9,#f_ulen]
 STR R0,[R9,#f_undo]
 STR R0,[R9,#f_undop]
 LDMFD R13!,{R1-R7,PC}

;E R9=file that has just has it's undo turned off/been killed
;X yank buffer has references to this file freed.

free_yank
 STMFD R13!,{R1-R3,R14}
 BL get_file_off		; R0=file off
 FNLDR R1,yank_ptr
 FNLDR R2,yank_len
 MVN R3,#0
 ADD R2,R1,R2
fr11
 CMP R1,R2
 LDMCSFD R13!,{R1-R3,PC}
 LDR R14,[R1],#8
 BIC R14,R14,#1<<30		; clear multi flag
 TEQ R14,R0
; was...
; BNE fr11
; MVN R14,#0
; STR R14,[R1,#-8]
; now...
 STREQ R3,[R1,#-8]		; if match
 B fr11

;E R9=file
;X undo switched off for this file #

turn_off_undo
 FNJSR
 LDR R0,[R9,#f_flags]
 BIC R0,R0,#1
 STR R0,[R9,#f_flags]
 BL new_file_title		; turn off the undo flag
 BLVC free_undo			; free all used undo buffers
 FNRTS

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; New undo protocol calls					;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;E R0=command number (block type)
;  R1=file offset (if needed)
;  R2=data length (if needed)
;  R3=data pointer (first block)
;  R4=data pointer (second block)
;X Command buffered with data inlined if b3=0 in R0 #

;buffer_command

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Manipulate size of undo buffers				;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;E R0=size you need the undo buffer to be
;  R9=file
;X Undo buffer at least this size.

ensure_undo_size
 FNJSR "R1"
 LDR R1,[R9,#f_blklen]
 LDR R14,[R9,#f_bufl]
 SUB R1,R1,R14			; current size
 CMP R0,R1
; FNRTS LT			; enough free
 ADDGE R0,R0,#ubuf_size		; want more free + extra
 BICGE R0,R0,#3			; word align
 BLGE set_undo_size
 FNRTS

;E R0=size of undo buffer wanted for this file (word aligned)
;  R9=file
;X Undo buffer area set to this size. #

set_undo_size
 FNJSR "R1"
 LDR R1,[R9,#f_bufl]
 ADD R1,R1,R0			; new size for this block
 BL get_file_off		; R0=file number
 BLVC set_block_size
 FNRTS

 END
