; Contains code called when things go bang
; $Id: Exceptions,fff,v 2.4 2002/11/12 00:06:01 christian Exp $

 GET h.ModuleBits
 GET h.ZapHeader
 GET h.StartUp
 GET h.Strings
 GET h.StartCode
 GET h.MiniBuff

 EXPORT register_abort_handlers

 EXPORT zephyr

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Exceptions							;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; register our exception handlers
; E
; X
register_abort_handlers
	FNJSR	"R0-R3,R8"
	ADD	R8,R12,#old_environment

	; keep these in order
	MOV	R0,#environ_Error
	ADR	R1,ErrorHandler
	FNLDR	R3,exception_buffer
	BL	change_environment

	MOV	R0,#environ_Exit
	ADR	R1,ExitHandler
	BL	change_environment

	MOV	R0,#environ_UpCall
	ADR	R1,UpCallHandler
	BL	change_environment

	BL	set_running_var
	FNRTS


; deregister our exception handlers
; E
; X
deregister_abort_handlers
	FNJSR	"R0-R4,R8"
	BL	unset_running_var

	; keep these in order
	ADD	R8,R12,#old_environment
	MOV	R0,#environ_Error
	BL	restore_environment
	MOV	R0,#environ_Exit
	BL	restore_environment
	MOV	R0,#environ_UpCall
	BL	restore_environment
	FNRTS


; UpCall hanlder
UpCallHandler
	TEQ	R0,#256			; New Application
	MOVEQ	R0,#0			; prevent it starting (use *WimpTask)
	MOV	PC,R14


; Error handler
ErrorHandler
	MOV	R12,R0			; get Zap workspace

	FNLDR	R13,env_start		; ensure we have a stack
	ADD	R13,R13,#stack_size

	FNLDR	R0,exception_buffer
	ADD	R0,R0,#4		; skip PC

	BL	kill_print_job
	BL	report_error
	BEQ	ExitHandler

	BL	checkanddump
	CLV
	BL	minibuffer_close
	B	main_loop


; Exit handler - tidy up system resources, remove handlers and exit
;
ExitHandler
	FNLDR	R13,env_start		; ensure we have a stack
	ADD	R13,R13,#stack_size

	BL	kill_print_job
	BL	deregister_abort_handlers
	BL	clean_up		; release our system resources

	ADR	R3,zap
	SWI	OS_ExitAndDie


; report the exception in a Wimp error box
; E r0 -> error message
;   exception_r14 contains PC at time of exception
; X Z set if user pressed Quit
report_error
	FNJSR
	[ LOGGING = SYSLOG
	ADD R0,R0,#4
	SYSLOGF "Exception '%0s'",LOG_EXCEPTION
	SUB R0,R0,#4
	]
	MOV	R1,#&500
	ADR	R2,zap
	ADR	R3,zap_sprite
	MOV	R4,#1			; use wimp area
	ADR	R5,buttons$l
	SWI	XWimp_ReportError
	TEQ	R1,#4
	FNRTS

zap_sprite
	=	"!"
zap	=	|zap$|,0

buttons$l
	=	"Continue,Quit",0	; FIXME: i18n
	ALIGN


	LOCAL

kill_print_job
	FNJSR	"R0-R1"
	FNLDR	R0,wimp_print_job
	TEQ	R0,#0
	SWINE	&A0149			; XPDriver_AbortJob
	FNLDR	R1,wimp_print_job
	TEQ	R1,#0
	MOVNE	R0,#0
	FNSTR	R0,wimp_print_job,NE
	SWINE	XOS_Find		; close file
	; ignore any errors - for now :-)
	FNLDR	R0,wimp_print_prevjob	; previous job
	TEQ	R0,#0
	MOVNE	R1,#0
	FNSTR	R1,wimp_print_prevjob,NE
	SWINE	&A0145			; XPDriver_SelectJob
	FNRTS


; do a coredump if the debug file is present
; E
; X
checkanddump
	FNJSR
	ADR	R1,debugfile$l
	MOV	R0,#5
	SWI	XOS_File		; check for <Zap$Dir>.Debug
	FNRTS	VS
	CMP	R0,#0
	BLNE	core_dump
	FNRTS

debugfile$l
	=	"<",|zap$|,"$Dir>.Debug",0
	ALIGN


	LOCAL

; setup an environment handler
; E r0-r1,r3 passed to OS_ChangeEnvironment
; X old
change_environment
	MOV	R2,R12
	SWI	XOS_ChangeEnvironment
	STMIA	R8!,{R1,R2,R3}
	MOV	PC,R14

; restore an environment handler
; E R8 -> block containing old handler
; X
restore_environment
	FNJSR
	LDMIA	R8!,{R1,R2,R3}
	SWI	XOS_ChangeEnvironment
	FNRTS


; set Zap$Running to "Yes"
; E
; X
set_running_var
	FNJSR	"R1-R4"
	ADR	R0,var$l
	ADR	R1,yes$l
	MOV	R2,#yes_end$l - yes$l
	MOV	R3,#0
	MOV	R4,#0			; string
	SWI	XOS_SetVarVal
	FNRTS

yes$l	=	"Yes",0
yes_end$l

; unset Zap$Running
; E
; X
unset_running_var
	FNJSR
	ADR	R0,var$l
	MOV	R1,#0
	MOV	R2,#-1			; delete
	MOV	R3,#0
	MOV	R4,#0
	SWI	XOS_SetVarVal
	FNRTS

var$l	=	|zap$|,"$Running",0
	ALIGN


; debugging code

zephyr

 [ LOGGING=ZEPHYR

zephyrmsg_blk
 DCD &20
 DCD &0
 DCD &0
 DCD &0
 DCD &804C0
 DCD &32323232
 DCD &32323232
 DCD &0

 FNJSR "R0-R6"
 ADR R1,zephyrmsg_blk + &14
 MOV R2,#8
 SWI XOS_ConvertHex8
 MOV R0,#&80000
 ORR R0,R0,#&4C0
 ADR R1,zephyrmsg_blk
 MOV R2,#0
 MOV R3,#32
 MOV R4,#0
 MOV R5,#0
 MOV R6,#0
 BL message_send
 FNRTS

 |

 MOV PC,R14

 ]

 END
